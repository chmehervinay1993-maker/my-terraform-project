locals {
public_subnets = zipmap(var.azs, var.public_subnet_cidrs)
private_subnets = zipmap(var.azs, var.private_subnet_cidrs)
nat_azs = (
var.enable_nat_gateway
? (var.single_nat_gateway ? [var.azs[0]] : var.azs)
: []
)
}
resource "aws_vpc" "this" {
cidr_block = var.cidr_block
enable_dns_support = true
enable_dns_hostnames = true
tags = merge(var.tags, { Name = var.name })
}
# Remove all rules from the default security group (nothing uses it).
resource "aws_default_security_group" "this" {
vpc_id = aws_vpc.this.id
tags = merge(var.tags, { Name = "${var.name}-default-deny" })
}
resource "aws_internet_gateway" "this" {
vpc_id = aws_vpc.this.id
tags = merge(var.tags, { Name = "${var.name}-igw" })
}
# ------------------------------------------------------------------ public
resource "aws_subnet" "public" {
for_each = local.public_subnets
vpc_id = aws_vpc.this.id
cidr_block = each.value
availability_zone = each.key
tags = merge(var.tags, {
Name = "${var.name}-public-${each.key}"
Tier = "public"
})
}
resource "aws_route_table" "public" {
vpc_id = aws_vpc.this.id
tags = merge(var.tags, { Name = "${var.name}-public" })
}
resource "aws_route" "public_internet" {
route_table_id = aws_route_table.public.id
destination_cidr_block = "0.0.0.0/0"
gateway_id = aws_internet_gateway.this.id
}
resource "aws_route_table_association" "public" {
for_each = aws_subnet.public
subnet_id = each.value.id
route_table_id = aws_route_table.public.id
}
# ------------------------------------------------------------------ NAT
resource "aws_eip" "nat" {
for_each = toset(local.nat_azs)
domain = "vpc"
tags = merge(var.tags, { Name = "${var.name}-nat-${each.key}" })
}
resource "aws_nat_gateway" "this" {
for_each = toset(local.nat_azs)
allocation_id = aws_eip.nat[each.key].id
subnet_id = aws_subnet.public[each.key].id
tags = merge(var.tags, { Name = "${var.name}-nat-${each.key}" })
depends_on = [aws_internet_gateway.this]
}
# ------------------------------------------------------------------ private
resource "aws_subnet" "private" {
for_each = local.private_subnets
vpc_id = aws_vpc.this.id
cidr_block = each.value
availability_zone = each.key
tags = merge(var.tags, {
Name = "${var.name}-private-${each.key}"
Tier = "private"
})
}
resource "aws_route_table" "private" {
for_each = aws_subnet.private
vpc_id = aws_vpc.this.id
tags = merge(var.tags, { Name = "${var.name}-private-${each.key}" })
}
resource "aws_route" "private_nat" {
for_each = var.enable_nat_gateway ? local.private_subnets : {}
route_table_id = aws_route_table.private[each.key].id
destination_cidr_block = "0.0.0.0/0"
nat_gateway_id = (
var.single_nat_gateway
? aws_nat_gateway.this[var.azs[0]].id
: aws_nat_gateway.this[each.key].id
)
}
resource "aws_route_table_association" "private" {
for_each = aws_subnet.private
subnet_id = each.value.id
route_table_id = aws_route_table.private[each.key].id
}
